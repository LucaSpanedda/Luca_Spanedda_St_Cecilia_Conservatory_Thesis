declare name "Agostino Di Scipio - AUDIBLE ECOSYSTEMICS n.2";
declare author "Luca Spanedda";
declare author "Dario Sanfilippo";
declare version "1.0";
declare description " 2023 version Realised on the 2017 composer's instructions";

// import faust standard library
import("stdfaust.lib");

// PERFORMANCE SYSTEM VARIABLES
SampleRate = 44100;
var1 = nentry("t:Control/h:System Variables/Var 1", 8.0, 1, 20, 1);
var2 = nentry("t:Control/h:System Variables/Var 2", 50, 1, 10000, 1);
var3 = nentry("t:Control/h:System Variables/Var 3", .25, 0, 1, .001);
var4 = nentry("t:Control/h:System Variables/Var 4", 8.2, 1, 20, 1);


//------- ------------- ----- -----------
//-- LIBRARY -------------------------------------------------------------------
//------- --------


//----------------------------------------------------------------- UTILITIES --
// limit function for library and system
limit(maxl,minl,x) = x : max(minl, min(maxl));

//-------------------------------------------------------------------- DELAYS --
delayfb(delSec,fb,x) = loop ~ _ : mem
with{ 
    loop(z) = ( (z * fb + x) @(max(0, ba.sec2samp(delSec)-1)) );
};

//---------------------------------------------------------------- SAMPLEREAD --
sampler(lengthSec, memChunk, ratio, x) = 
    it.frwtable(3, 20 * SampleRate, .0, writePtr, x, readPtr) * window
    with {
        memChunkLimited = max(0.010, min(1, memChunk));
        bufferLen = lengthSec * SampleRate;
        writePtr = ba.period(bufferLen);
        grainLen = max(1, ba.if(writePtr > memChunkLimited * bufferLen, 
            memChunkLimited * bufferLen, 1));
        readPtr = y
            letrec {
                'y = (ratio + y) % grainLen;
            };
        window = min(1, abs(((readPtr + grainLen / 2) % grainLen) - 
            grainLen / 2) / 200);
    };

//--------------------------------------------------------------- INTEGRATOR ---
integrator(seconds, x) = an.abs_envelope_tau(limit(1000,.001,seconds), x);

//----------------------------------------------------------------- LOCALMAX ---
localMax(seconds, x) = loop ~ si.bus(4) : _ , ! , ! , !
with {
    loop(yState, timerState, peakState, timeInSamplesState) = 
        y , timer , peak , timeInSamples
    with {
        timeInSamples = ba.if(reset + 1 - 1', seconds * 
            ma.SR, timeInSamplesState);
        reset = timerState >= (timeInSamplesState - 1);
        timer = ba.if(reset, 1, timerState + 1);
        peak = max(abs(x), peakState * (1.0 - reset));
        y = ba.if(reset, peak', yState);
    };
};

localmax(resetPeriod, x) = localMax(limit(1000,0,resetPeriod), x);

//----------------------------------------------------------------- TRIANGLE ---
triangularFunc(x) = abs(ma.frac((x - .5)) * 2.0 - 1.0);
triangleWave(f) = triangularFunc(os.phasor(1,f));

//------------------------------------------------------------------ FILTERS ---
onePoleTPT(cf, x) = loop ~ _ : ! , si.bus(3)
with {
    g = tan(cf * ma.PI * (1/ma.SR));
    G = g / (1.0 + g);
    loop(s) = u , lp , hp , ap
    with {
        v = (x - s) * G;
        u = v + lp;
        lp = v + s;
        hp = x - lp;
        ap = lp - hp;
    };
};

SVFTPT(Q, cf, x) = loop ~ si.bus(2) : (! , ! , _ , _ , _ , _ , _)
    with {
        g = tan(cf * ma.PI * (1.0/ma.SR));
        R = 1.0 / (2.0 * Q);
        G1 = 1.0 / (1.0 + 2.0 * R * g + g * g);
        G2 = 2.0 * R + g;
        loop(s1, s2) = u1 , u2 , lp , hp , bp , bp * 2.0 * R , x - bp * 4.0 * R
            with {
                hp = (x - s1 * G2 - s2) * G1;
                v1 = hp * g;
                bp = s1 + v1;
                v2 = bp * g;
                lp = s2 + v2;
                u1 = v1 + bp;
                u2 = v2 + lp;
            };
    };

SVFTPT2(K, Q, CF, x) = circuitout : ! , ! , _ , _ , _ , _ , _ , _ , _ , _
with{
    g = tan(CF * ma.PI / ma.SR);
    R = 1.0 / (2.0 * Q);
    G1 = 1.0 / (1.0 + 2.0 * R * g + g * g);
    G2 = 2.0 * R + g;
    circuit(s1, s2) = u1 , u2 , lp , hp , bp, notch, apf, ubp, peak, bshelf
        with{
            hp = (x - s1 * G2 - s2) * G1;
            v1 = hp * g;
            bp = s1 + v1;
            v2 = bp * g;
            lp = s2 + v2;
            u1 = v1 + bp;
            u2 = v2 + lp;
            notch = x - ((2*R)*bp);
            apf = x - ((4*R)*bp);
            ubp = ((2*R)*bp);
            peak = lp -hp;
            bshelf = x + (((2*K)*R)*bp);
        };
    // choose the output from the SVF Filter (ex. bshelf)
    circuitout = circuit ~ si.bus(2);
};

LPTPT(CF, x) = onePoleTPT(max(ma.EPSILON, min(20480, CF)), x) : (_ , ! , !);
HPTPT(CF, x) = onePoleTPT(max(ma.EPSILON, min(20480, CF)), x) : (! , _ , !);

LPSVF(Q, CF, x) = SVFTPT2(0, Q, max(ma.EPSILON, min(20480, CF)), x) : _ , ! , ! , ! , ! , ! , ! , ! ;
HPSVF(Q, CF, x) = SVFTPT2(0, Q, max(ma.EPSILON, min(20480, CF)), x) : ! , _ , ! , ! , ! , ! , ! , !;

BPsvftpt(bw, cf, x) = Q , CF , x : SVFTPT : (! , ! , ! , _ , !)
    with {
        CF = max(20, min(20480, LPTPT(1, cf)));
        BW = max(1, min(20480, LPTPT(1, bw)));
        Q = max(.01, min(100, BW / CF));
    };

// Butterworth
butterworthQ(order, stage) = qFactor(order % 2)
with {
    qFactor(0) = 1.0 / (2.0 * cos(((2.0 * stage + 1) *
    (ma.PI / (order * 2.0)))));
    qFactor(1) = 1.0 / (2.0 * cos(((stage + 1) * (ma.PI / order))));
};

LPButterworthN(1, cf, x) = LPTPT(cf, x);
LPButterworthN(N, cf, x) = cascade(N % 2)
with {
    cascade(0) = x : seq(i, N / 2, LPSVF(butterworthQ(N, i), cf));
    cascade(1) = x : LPTPT(cf) : seq(i, (N - 1) / 2,
    LPSVF(butterworthQ(N, i), cf));
};

HPButterworthN(1, cf, x) = HPTPT(cf, x);
HPButterworthN(N, cf, x) = cascade(N % 2)
with {
    cascade(0) = x : seq(i, N / 2, HPSVF(butterworthQ(N, i), cf));
    cascade(1) = x : HPTPT(cf) : seq(i, (N - 1) /
    2, HPSVF(butterworthQ(N, i), cf));
};

// Filters Order Butterworth
LP1(CF, x) = x : LPButterworthN(1, CF);
HP1(CF, x) = x : HPButterworthN(1, CF);
LP2(CF, x) = x : LPButterworthN(2, CF); 
HP2(CF, x) = x : HPButterworthN(2, CF); 
LP3(CF, x) = x : LPButterworthN(3, CF); 
HP3(CF, x) = x : HPButterworthN(3, CF); 
LP4(CF, x) = x : LPButterworthN(4, CF);
HP4(CF, x) = x : HPButterworthN(4, CF); 
LP5(CF, x) = x : LPButterworthN(5, CF); 
HP5(CF, x) = x : HPButterworthN(5, CF); 

// Filters Order in series
// LP1(CF, x) = x : LPTPT(CF);
// HP1(CF, x) = x : HPTPT(CF);
// LP2(CF, x) = x : LPTPT(CF) : LPTPT(CF);
// HP2(CF, x) = x : HPTPT(CF) : HPTPT(CF);
// LP3(CF, x) = x : LPTPT(CF) : LPTPT(CF) : LPTPT(CF);
// HP3(CF, x) = x : HPTPT(CF) : HPTPT(CF) : HPTPT(CF);
// LP4(CF, x) = x : LPTPT(CF) : LPTPT(CF) : LPTPT(CF) : LPTPT(CF);
// HP4(CF, x) = x : HPTPT(CF) : HPTPT(CF) : HPTPT(CF) : HPTPT(CF);
// LP5(CF, x) = x : LPTPT(CF) : LPTPT(CF) : LPTPT(CF) : LPTPT(CF) : LPTPT(CF);
// HP5(CF, x) = x : HPTPT(CF) : HPTPT(CF) : HPTPT(CF) : HPTPT(CF) : HPTPT(CF);

//-------------------------------------------------------- GRANULAR SAMPLING ---
grain(L, position, duration, x, trigger) = hann(phase) * buffer(readPtr, x)
with {
    maxLength = 20 * SampleRate;
    length = L * SampleRate;
    hann(ph) = sin(ma.PI * ph) ^ 2.0;
    lineSegment = loop ~ si.bus(2) : _ , ! , _
    with {
        loop(yState, incrementState) = y , increment , ready
        with {
            ready = ((yState == 0.0) | (yState == 1.0)) & trigger;
            y = ba.if(ready, increment, min(1.0, yState + increment));
            increment = ba.if(ready, (1.0/ma.SR) / max((1.0/ma.SR), duration), 
                incrementState);
        };
    };
    phase = lineSegment : _ , !;
    unlocking = lineSegment : ! , _;
    lock(param) = ba.sAndH(unlocking, param); 
    grainPosition = lock(position);
    grainDuration = lock(duration);
    readPtr = grainPosition * length + phase * grainDuration * ma.SR;
    buffer(readPtr, x) = 
        it.frwtable(3, maxLength, .0, writePtr, x, readPtrWrapped)
    with {
        writePtr = ba.period(length);
        readPtrWrapped = ma.modulo(readPtr, length);
    };
};

// works for N >= 2
triggerArray(N, rate) = loop ~ si.bus(3) : (! , ! , _) <: 
    par(i, N, == (i)) : par(i, N, \(x).(x > x'))
with {
    loop(incrState, phState, counterState) = incr , ph , counter
    with {
        init = 1 - 1';
        trigger = (phState < phState') + init;
        incr = ba.if(trigger, rate * (1.0/ma.SR), incrState);
        ph = ma.frac(incr + phState);
        counter = (trigger + counterState) % N;
    };
};

grainN(voices, L, position, rate, duration, x) = triggerArray(voices, rate) : 
    par(i, voices, grain(L, position, duration, x));

granular_sampling(var1, timeIndex, memWriteDel, cntrlLev, divDur, x) = 
    grainN(10, var1, position, rate, duration, x) :> _
with {
    rnd = no.noise;
    memPointerJitter = rnd * (1.0 - memWriteDel) * .01;
    position = timeIndex * (1.0 - ((1.0 - memWriteDel) * .01)) + 
        memPointerJitter;
    density = 1.0 - cntrlLev;
    rate = 50 ^ (density * 2.0 - 1.0);
    grainDuration = .023 + (1.0 - memWriteDel) / divDur;
    duration = grainDuration + grainDuration * .1 * rnd;
};

//---------------------------------------------------------------------- GUI ---
VHmetersEnvelope = abs : max ~ -(1.0/ma.SR) : 
    max(ba.db2linear(-84)) : ba.linear2db;

inspect(i, lower, upper) = _ * 1000 <: _ , 
    vbargraph("sig_%2i [style:numerical]", lower * 1000, upper * 1000) : 
        attach : _ / 1000;

diffDebug(x) = an.abs_envelope_tau(1, (x-x')) * (SampleRate/2);

SF2Ainspect = tgroup("Control", vgroup("System Inspectors", par(i, 8, hgroup("Signal Flow 2a [23]", _ : inspect(i+1, -1, 1)))));
SF1Binspect = tgroup("Control", vgroup("System Inspectors", par(i, 8, hgroup("Signal Flow 1b [22]", _ : inspect(i+1, -1, 1)))));
SF1Ainspect = tgroup("Control", vgroup("System Inspectors", par(i, 8, hgroup("Signal Flow 1a [21]", _ : inspect(i+1, -1, 1)))));
SF3Oinspect = tgroup("Control", vgroup("System Inspectors", par(i, 6, hgroup("Signal Flow  3 [24]", _ : inspect(i+1, -1, 1)))));

GSinspect(i, x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Internal/h:Granular Sampling/GS %i Gain [unit:db]", 0, -84, 24, .001))) <: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Internal/h:Granular Sampling/GS %i [unit:dB]", -80, 20));

SRinspect(i, x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Internal/h:Sample Read/SR %i Gain [unit:db]", 0, -84, 24, .001))) <: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Internal/h:Sample Read/SR %i [unit:dB]", -80, 20));

gainMic_1A1(x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Mixer/h:Signal Flow 1A/Mic 3 [10][unit:db]", 0, -84, 24, .001))) <: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Mixer/h:Signal Flow 1A/Mic 3 VH [unit:dB]", -80, 20));  
gainMic_1A2(x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Mixer/h:Signal Flow 1A/Mic 4 [11][unit:db]", 0, -84, 24, .001))) <: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Mixer/h:Signal Flow 1A/Mic 4 VH [unit:dB]", -80, 20)); 
gainMic_1B1(x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Mixer/h:Signal Flow 1B/Mic 1 [12][unit:db]", 0, -84, 24, .001))) <: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Mixer/h:Signal Flow 1B/Mic 1 VH [unit:dB]", -80, 20));  
gainMic_1B2(x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Mixer/h:Signal Flow 1B/Mic 2 [13][unit:db]", 0, -84, 24, .001))) <: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Mixer/h:Signal Flow 1B/Mic 2 VH [unit:dB]", -80, 20)); 
gainMic_2A1(x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Mixer/h:Signal Flow 2A/Mic 1 [14][unit:db]", 0, -84, 24, .001)))<: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Mixer/h:Signal Flow 2A/Mic 1 VH [unit:dB]", -80, 20));  
gainMic_2A2(x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Mixer/h:Signal Flow 2A/Mic 2 [15][unit:db]", 0, -84, 24, .001)))<: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Mixer/h:Signal Flow 2A/Mic 2 VH [unit:dB]", -80, 20));  
gainMic_3O1(x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Mixer/h:Signal Flow  3/Out1 [16][unit:db]", 0, -84, 24, .001)))<: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Mixer/h:Signal Flow  3/Out1 VH [unit:dB]", -80, 20));   
gainMic_3O2(x) = x * si.smoo(ba.db2linear(vslider("t:Control/h:Mixer/h:Signal Flow  3/Out2 [17][unit:db]", 0, -84, 24, .001)))<: 
    attach(_, VHmetersEnvelope(_) : vbargraph("t:Control/h:Mixer/h:Signal Flow  3/Out2 VH [unit:dB]", -80, 20));   

//---------------------------------- TEST SIGNALS (Like Decorrelated Larsen) ---
noise(initSeed) = LCG ~ _ : (_ / m)
with{
    // variables
    // initSeed = an initial seed value
    a = 18446744073709551557; // a large prime number
    c = 12345; // a small prime number, such as 12345
    m = 2 ^ 31; // 2.1 billion
    // linear_congruential_generator
    LCG(seed) = ((a * seed + c) + (initSeed-initSeed') % m);
};

Test(N) = par(i, N, fi.bandpass(4, 1000, 1020, noise((i+1) * 469762049)) * 400);